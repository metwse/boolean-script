Unicode font used in this file is math regular italic. 𝜖 used for empty leaves.


```
GENERAL
    /* bit literal */
    𝑏𝑖𝑡 → 0
        | 1

    𝑝𝑜𝑠𝑖𝑡𝑖𝑣𝑒_𝑖𝑛𝑡 → /[1-9][0-9]*/

    /* identifier */
    𝑖𝑑𝑒𝑛𝑡 → /[a-zA-Z_][0-9a-zA-Z_]*/

    𝑖𝑑𝑒𝑛𝑡_𝑜𝑟_𝑚𝑒𝑚𝑏𝑒𝑟 → 𝑖𝑑𝑒𝑛𝑡 . 𝑝𝑜𝑠𝑖𝑡𝑖𝑣𝑒_𝑖𝑛𝑡
                    | 𝑖𝑑𝑒𝑛𝑡

    𝑡𝑦 → bool
       | vec < 𝑝𝑜𝑠𝑖𝑡𝑖𝑣𝑒_𝑖𝑛𝑡 >
```
```
EXPRESSIONS
    /* expression */
    𝑒𝑥𝑝𝑟 → 𝑡𝑒𝑟𝑚 𝑒𝑥𝑝𝑟_𝑟𝑒𝑠𝑡

    𝑒𝑥𝑝𝑟_𝑟𝑒𝑠𝑡 → + 𝑒𝑥𝑝𝑟 𝑒𝑥𝑝𝑟_𝑟𝑒𝑠𝑡
              | 𝜖

    𝑡𝑒𝑟𝑚 → 𝑓𝑎𝑐𝑡𝑜𝑟 𝑡𝑒𝑟𝑚_𝑟𝑒𝑠𝑡

    𝑡𝑒𝑟𝑚_𝑟𝑒𝑠𝑡 → * 𝑡𝑒𝑟𝑚 𝑡𝑒𝑟𝑚_𝑟𝑒𝑠𝑡
              | 𝜖

    𝑓𝑎𝑐𝑡𝑜𝑟 → 𝑎𝑡𝑜𝑚 𝑜𝑝𝑡𝑖𝑛𝑣𝑜𝑙𝑢𝑡𝑖𝑜𝑛

    𝑜𝑝𝑡𝑖𝑛𝑣𝑜𝑙𝑢𝑡𝑖𝑜𝑛 → '
                  | 𝜖

    𝑎𝑡𝑜𝑚 → ( 𝑒𝑥𝑝𝑟 )
         | [ 𝑒𝑥𝑝𝑟_𝑙𝑠 ]
         | ( 𝑎𝑠𝑛𝑔 )
         | 𝑐𝑎𝑙𝑙
         | 𝑖𝑑𝑒𝑛𝑡_𝑜𝑟_𝑚𝑒𝑚𝑏𝑒𝑟
         | 𝑏𝑖𝑡

    𝑐𝑎𝑙𝑙 → 𝑖𝑑𝑒𝑛𝑡 ( 𝑜𝑝𝑡𝑝𝑎𝑟𝑎𝑚𝑠 )

    𝑜𝑝𝑡𝑝𝑎𝑟𝑎𝑚𝑠 → 𝑒𝑥𝑝𝑟_𝑙𝑠
              | 𝜖
```
```
STATEMENTS
    /**
     * statement
     * 𝑑𝑒𝑐𝑙 MUST matched before 𝑒𝑥𝑝𝑟
     */
    𝑠𝑡𝑚𝑡 → 𝑑𝑒𝑐𝑙 𝑑𝑒𝑙𝑖𝑚
         | 𝑎𝑠𝑛𝑔 𝑑𝑒𝑙𝑖𝑚
         | 𝑒𝑥𝑝𝑟 𝑑𝑒𝑙𝑖𝑚
         | { 𝑠𝑡𝑚𝑡𝑠 }

    /* block statements */
    𝑠𝑡𝑚𝑡𝑠 → 𝑠𝑡𝑚𝑡 𝑠𝑡𝑚𝑡𝑠
          | 𝜖

    /* variable declaration */
    𝑑𝑒𝑐𝑙 → 𝑡𝑦 𝑖𝑑𝑒𝑛𝑡_𝑙𝑠 𝑑𝑒𝑐𝑙_𝑜𝑝𝑡𝑎𝑠𝑛𝑔
         | 𝑖𝑑𝑒𝑛𝑡_𝑙𝑠 𝑑𝑒𝑐𝑙_𝑜𝑝𝑡𝑎𝑠𝑛𝑔

    𝑑𝑒𝑐𝑙_𝑜𝑝𝑡𝑎𝑠𝑛𝑔 → = 𝑎𝑠𝑛𝑔
                 | = 𝑒𝑥𝑝𝑟_𝑙𝑠
                 | 𝜖

    𝑑𝑒𝑙𝑖𝑚 → ;
          | no_escape_newline
```
```
LISTS
    /* asgn is also a statement */
    𝑎𝑠𝑛𝑔 → 𝑖𝑑𝑒𝑛𝑡_𝑜𝑟_𝑚𝑒𝑚𝑏𝑒𝑟_𝑙𝑠 𝑎𝑠𝑛𝑔_𝑟𝑒𝑠𝑡 = 𝑒𝑥𝑝𝑟_𝑙𝑠

    𝑎𝑠𝑛𝑔_𝑟𝑒𝑠𝑡 → = 𝑖𝑑𝑒𝑛𝑡_𝑜𝑟_𝑚𝑒𝑚𝑏𝑒𝑟_𝑙𝑠 𝑎𝑠𝑛𝑔_𝑟𝑒𝑠𝑡
              | 𝜖

    𝑖𝑑𝑒𝑛𝑡_𝑙𝑠 → 𝑖𝑑𝑒𝑛𝑡 𝑖𝑑𝑒𝑛𝑡_𝑙𝑠_𝑟𝑒𝑠𝑡

    𝑖𝑑𝑒𝑛𝑡_𝑙𝑠_𝑟𝑒𝑠𝑡 → , 𝑖𝑑𝑒𝑛𝑡 𝑖𝑑𝑒𝑛𝑡_𝑙𝑠_𝑟𝑒𝑠𝑡
                  | 𝜖

    𝑖𝑑𝑒𝑛𝑡_𝑜𝑟_𝑚𝑒𝑚𝑏𝑒𝑟_𝑙𝑠 → 𝑖𝑑𝑒𝑛𝑡_𝑜𝑟_𝑚𝑒𝑚𝑏𝑒𝑟 𝑖𝑑𝑒𝑛𝑡_𝑜𝑟_𝑚𝑒𝑚𝑏𝑒𝑟_𝑙𝑠_𝑟𝑒𝑠𝑡

    𝑖𝑑𝑒𝑛𝑡_𝑜𝑟_𝑚𝑒𝑚𝑏𝑒𝑟_𝑙𝑠_𝑟𝑒𝑠𝑡 → , 𝑖𝑑𝑒𝑛𝑡_𝑜𝑟_𝑚𝑒𝑚𝑏𝑒𝑟 𝑖𝑑𝑒𝑛𝑡_𝑜𝑟_𝑚𝑒𝑚𝑏𝑒𝑟_𝑙𝑠_𝑟𝑒𝑠𝑡
                            | 𝜖

    𝑒𝑥𝑝𝑟_𝑙𝑠 → 𝑒𝑥𝑝𝑟 𝑒𝑥𝑝𝑟_𝑙𝑠_𝑟𝑒𝑠𝑡

    𝑒𝑥𝑝𝑟_𝑙𝑠_𝑟𝑒𝑠𝑡 → , 𝑒𝑥𝑝𝑟 𝑒𝑥𝑝𝑟_𝑙𝑠_𝑟𝑒𝑠𝑡
                 | 𝜖
```
